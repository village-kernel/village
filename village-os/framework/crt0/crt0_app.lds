/* Script for -pie */
ENTRY(_start)

SECTIONS
{
	PROVIDE (__executable_start = SEGMENT_START("text-segment", 0)); 
	. = SEGMENT_START("text-segment", 0) + SIZEOF_HEADERS;
	
	_IMGOFFS = .;
	
	_sivector = LOADADDR(.entry);

	.entry :
	{
		. = ALIGN(4);
		_svector = .;
		KEEP(*(.entry))
		. = ALIGN(4);
		_evector = .;
	}

	.text :
	{
		. = ALIGN(4);
		*(.text)
		*(.text*)

		KEEP (*(.init))
		KEEP (*(.fini))

		. = ALIGN(4);
		 _etext = .; 
	}

	.rodata :
	{
		. = ALIGN(4);
		*(.rodata)
		*(.rodata*)
		*(.gnu.linkonce.r.*)
		. = ALIGN(4);
	}

	.x86.extab : { *(.gcc_except_table.*  .got.plt ) }
	.x86 : {
		__exidx_start = .;
		*(.x86.extab*)
		__exidx_end = .;
	}

	.preinit_array :
	{
		PROVIDE_HIDDEN (__preinit_array_start = .);
		KEEP (*(SORT(.preinit_array.*)))
		KEEP (*(.preinit_array*))
		PROVIDE_HIDDEN (__preinit_array_end = .);
	}

	.init_array :
	{
		PROVIDE_HIDDEN (__init_array_start = .);
		KEEP (*(SORT(.init_array.*)))
		KEEP (*(.init_array*))
		KEEP (*(.ctors*))
		PROVIDE_HIDDEN (__init_array_end = .);
	}

	.fini_array :
	{
		PROVIDE_HIDDEN (__fini_array_start = .);
		KEEP (*(SORT(.fini_array.*)))
		KEEP (*(.fini_array*))
		KEEP (*(.dtors*))
		PROVIDE_HIDDEN (__fini_array_end = .);
	}

	_sidata = LOADADDR(.data);

	.data :
	{
		. = ALIGN(4);
		_sdata = .;  
		*(.data .data.* .gnu.linkonce.d.*)
		. = ALIGN(4);
		_edata = .; 
	}

	.bss :
	{
		. = ALIGN(4);
		_sbss = .; 
		__bss_start__ = _sbss;
		*(.dynbss)
		*(.bss .bss.* .gnu.linkonce.b.*)
		*(COMMON)

		. = ALIGN(4);
		_ebss = .; 
		 __bss_end__ = _ebss;
	}

	/*/DISCARD/ :
	{
		libc.a ( * )
		libm.a ( * )
		libgcc.a ( * )
	}*/
}
